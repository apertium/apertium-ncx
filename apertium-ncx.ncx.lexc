!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

Multichar_Symbols

%<n%>                                    ! Sustantivo
%<v%>                                    ! Verbo
%<adj%>                                  ! Adjetivo
%<det%>                                  ! Determinante
%<num%>                                  ! Número
%<prn%>                                  ! Pronombre
%<adv%>                                  ! Adverbio
%<np%>                                   ! Nombre propio
%<cnjsub%>                               ! Coordinación subordinada

%<pr%>                                   ! Preposición

%<tv%>                                   ! Transitivo
%<tv2%>                                  ! Bitransitivo
%<iv%>                                   ! Intransitivo

%<pl%>                                   ! Plural
%<sg%>                                   ! Singular

%<abs%>                                  ! Absolutivo

%<loc%>                                  ! Locativo

%<top%>                                  ! Topónimo

%<pro%>                                  ! Pronombre proclítico
%<pers%>                                 ! Personal, forma larga

%<dim%>                                  ! Diminutivo

%<hon%>                                  ! Honorifico

%<barb%>                                 ! Barbarismo (español)


%<pres%>   ! tiempo presente
%<pret%>   ! pretérito
%<perf%>   ! perfectivo
%<impf%>   ! imperfectivo
%<fut%>    ! futuro
%<plu%>    ! pluscuamperfecto
%<opt%>    ! optativo
%<pst%>    ! pasado
%<dur%>    ! aspecto durativo

%<p1%>
%<p2%>
%<p3%>

%<px1sg%>                                ! Posesivo (1ra singular)
%<px2sg%>
%<px3sg%>
%<px1pl%>
%<px2pl%>
%<px3pl%>
%<px_nn%>				 ! "te-"

%<s_sg1%>                                ! Marcador de sujeto (1ra singular)
%<s_sg2%>                                ! Marcador de sujeto (2da singular)
%<s_sg3%>
%<s_pl1%>
%<s_pl2%>
%<s_pl3%>

%<o_sg1%>                               ! Marcador de objeto directo (1ra singular)
%<o_sg2%>                               ! Marcador de objeto directo (2da singular)
%<o_sg3%>
%<o_pl1%>
%<o_pl2%>
%<o_pl3%>

%<i_sg1%>                               ! Marcador de objeto indirecto (1ra singular)
%<i_sg2%>                               ! Marcador de objeto indirecto (2da singular)
%<i_sg3%>
%<i_pl1%>
%<i_pl2%>
%<i_pl3%>

%<o_aa3%>
%<o_nn3%>

%<ven%>                           ! direccional hacia el locutor
%<and%>				  ! direccional no hacia el locutor

%<afirm%>
%<ant%>
%<appl%>
%<aux%>
%<cfact%>
%<cni%>
%<cm%>
%<cnjcoo%>
%<f%>
%<guio%>
%<ij%>
%<lpar%>
%<lquest%>
%<lquot%>
%<m%>
%<neg%>
%<o_sg3_om%>
%<part%>
%<refl%>
%<rel%>
%<rpar%>
%<rquot%>
%<bien%>
%<juntos%>
%<completamente%>
%<prx%>  ! Proximal ʼthisʼ
%<med%>  ! Medial "that"
%<dst%>  ! Distal "that (over there)"

%<sent%>                                 ! Final de frase

%{o%}                                    ! o 
%{i%}                                    ! i / 0
%{e%}                                    ! e / 0
%{h%}                                    ! h / u
%{u%}                                    ! u / h
%{H%}
%{m%}                                    ! m / 0
%{M%}                                    ! m / n
%{q%}
%{c%}
%{s%}                                    ! para plural español
%{z%}                                    ! z/c
%{e%}
%{%@%}                                   ! para terminaciones -o y -a en sustantivos y/o adjetivos

%>                                       ! Frontera de morfema


@U.Subj.PL@





!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON Root

Nominal ;
Aumento ;

!Determinantes ;
!Numerales ; 
!Propios ;
!Adverbios ;
!Preposiciones ;
!Puntuación ;
!Interjecciones ; 
!Verb ;
! Digits ;



LEXICON Nominal

Pronombre ;
SujetoNominal ;

LEXICON Pronombre

PronombrePersonal ;
PronombreDemonstrativo ;


LEXICON PronombrePersonal

! Singular
nejuatl%<prn%>%<pro%>%<p1%>%<sg%>:ne # ;
nejuatl%<prn%>%<pers%>%<p1%>%<sg%>:nejuatl # ;
tejuatl%<prn%>%<pro%>%<p2%>%<sg%>:te # ;
tejuatl%<prn%>%<pers%>%<p2%>%<sg%>:tejuatl # ;
yejuatl%<prn%>%<pro%>%<p3%>%<sg%>:ye # ;
yejuatl%<prn%>%<pers%>%<p3%>%<sg%>:yejuatl # ;

! Plural
tejuan%<prn%>%<pers%>%<p1%>%<pl%>:tejuan # ;
namejuan%<prn%>%<pers%>%<p2%>%<pl%>:namejuan # ;
yejuan%<prn%>%<pers%>%<p3%>%<pl%>:yejuan # ;

LEXICON PronombreDemonstrativo
nin%<prn%>%<prx%>:nin # ; ! "esto"
nin%<prn%>%<prx%>%<pl%>:nin%>mej # ; ! "estos"

non%<prn%>%<med%>:non # ; ! "eso"
non%<prn%>%<med%>%<pl%>:non%>mej # ; ! "esos"

neka%<prn%>%<dst%>:neka # ; ! "aquello"
neka%<prn%>%<dst%>%<pl%>:nekatej # ; ! "aquellos"


LEXICON SujetoNominal

%<s_sg1%>:ni%>  PrefijoPosesivo ;
%<s_sg2%>:ti%>  PrefijoPosesivo ;
%<s_sg3%>:0     PrefijoPosesivo ;
%<s_pl1%>:ti%>  PrefijoPosesivo ;
%<s_pl2%>:nan%> PrefijoPosesivo ;
%<s_pl3%>:0     PrefijoPosesivo ;

LEXICON PrefijoPosesivo
Sustantivo ;
%<px1sg%>:no%> Sustantivo ;
%<px2sg%>:mo%> Sustantivo ;
%<px3sg%>:i%> Sustantivo ;
%<px1pl%>:to%> Sustantivo ;
%<px2pl%>:namo%> Sustantivo ;
%<px3pl%>:in%{m%}%{i%}%> Sustantivo ;

%<px_nn%>:te Sustantivo ; ! Todo: figure out correct tag

LEXICON PluralNominal
%<abs%>%<pl%>:%>mej # ;
%<pl%>:%>uan        # ;

LEXICON TratoFamiliar
%<dim%>:%>tsin # ;
%<dim%>:%>tsin%>tli # ;
%<dim%>%<pl%>:%>tsi%>tsin # ;
%<dim%>%<pl%>:%>tsin%>uan # ; ! poseído


LEXICON TL
%<abs%>%<sg%>:%>tl # ;
%<sg%>:0 # ; ! poseído TODO: flag diacritics ftw
TratoFamiliar ;
PluralNominal ;

LEXICON TLI
%<abs%>%<sg%>:%>tli # ;
%<sg%>:0 # ;
TratoFamiliar ;
PluralNominal ;

LEXICON LI
%<abs%>%<sg%>:%>i # ;
%<sg%>:0 # ;
TratoFamiliar ;
PluralNominal ;

LEXICON NA
%<sg%>:0 # ;
TratoFamiliar ;
PluralNominal ;

LEXICON Sustantivo
axka:axka NA; ! "partícula posesiva"

tlaxkali:tlaxkal LI ; ! "tortilla"

askatl:aska      TL ; ! "hormiga"

tixtli:tix	 TLI ; ! "masa"

LEXICON Aumento

:o%> SujetoVerbal ;
SujetoVerbal ;


LEXICON SujetoVerbal

%<s_sg1%>:ni%>  	SujetoCont ;
%<s_sg2%>:ti%>  	SujetoCont ;
%<s_sg3%>:0     	SujetoCont ;
%<s_pl1%>:ti%>          SujetoCont ;
%<s_pl2%>:nan%> 	SujetoCont ;
%<s_pl3%>:0             SujetoCont ;

LEXICON SujetoCont
IV-Dir ;
Objeto ;

LEXICON Objeto
DV-IO ;
TV-Dir ;
DV-Dir ;
%<o_sg1%>:nech%>    TV-Dir ;
%<o_sg2%>:mits%>    TV-Dir ;
%<o_sg3%>:k%{i%}%>  TV-Dir ;
%<o_pl1%>:tech%>    TV-Dir ;
%<o_pl2%>:mech%>    TV-Dir ;
%<o_pl2%>:namech%>  TV-Dir ; ! con sujeto de tercera persona.
%<o_pl3%>:ki%{m%}%> TV-Dir ;

LEXICON DV-IO
%<i_sg1%>:nech%>    DV-O ;
%<i_sg2%>:mits%>    DV-O ;
%<i_sg3%>:k%{i%}%>  DV-O ;
%<i_pl1%>:tech%>    DV-O ;
%<i_pl2%>:mech%>    DV-O ;
%<i_pl2%>:namech%>  DV-O ; ! con sujeto de tercera persona.
%<i_pl3%>:ki%{m%}%> DV-O ;

LEXICON IV-Dir
%<and%>:on IV-Adv ;
%<ven%>:ual IV-Adv ;
IV-Adv ;

LEXICON TV-Dir
%<and%>:on TV-Refl ;
%<ven%>:ual TV-Refl ;
TV-Refl ;

LEXICON DV-O
! for direct obj "im-" ... is this a thing in ncx?
DV-Dir ;


LEXICON DV-Dir
%<and%>:on DV-Refl ;
%<ven%>:ual DV-Refl ;
DV-Refl ;

LEXICON TV-Refl
%<refl%>:mo%>  TV-Adv ;
TV-Adv ;


LEXICON DV-Refl
%<refl%>:mo%>  DV-Adv ;
DV-Adv ;

!TODO: resolver dudas sobre la posicón y función de te- y tla- en ncx
LEXICON TV-Te
%<o_aa3%>:te%> TV-Raiz ;
TV-Raiz ;

LEXICON DV-Te
%<o_aa3%>:te%> DV-Raiz ;

LEXICON TV-Tla
%<o_nn3%>:tla%> TV-Raiz ;

LEXICON DV-Tla
%<o_nn3%>:tla%> DV-Raiz ;

LEXICON Te-Tla
%<i_aa3%>%<o_nn3%>:te%>tla%> DV-Raiz ;


LEXICON IV-Adv
%<nan%>:yek IV-Raiz ;
%<nan%>:sen IV-Raiz ;
! ...etc.
IV-Raiz ;

LEXICON TV-Adv
%<nan%>:yek TV-Adv-Cont ;
%<nan%>:sen TV-Adv-Cont ;
! ...etc.
TV-Adv-Cont ;

LEXICON TV-Adv-Cont
TV-Tla ;
TV-Te ;
TV-Raiz ;

LEXICON DV-Adv
%<nan%>:yek DV-Adv-Cont ;
%<nan%>:sen DV-Adv-Cont ;
! ...etc.
DV-Adv-Cont ;

LEXICON DV-Adv-Cont
Te-Tla  ;
DV-Te   ;
DV-Tla  ;
DV-Raiz ;

LEXICON Purposive ! en spanish?
%<aux%>:%>ti # ;
%<aux%>:%>to # ;
%<aux%>%<pret%>:%>ki # ;
%<aux%>%<pret%>:%>ko # ;
%<aux%>:%>ti%>j # ;
%<aux%>:%>to%>j # ;
%<aux%>%<pret%>:%>ki%>j # ;
%<aux%>%<pret%>:%>ko%>j # ;

LEXICON TAM-Clase1
%<pres%>:0 # ;
%<pres%>:%>j # ; ! TODO: flag diacritics for plural subj
%<pret%>:k # ;
%<pret%>:%>kej # ;
%<impf%>:%>ya # ;
%<impf%>:%>ya%>j # ;
%<fut%>:%>s # ;
%>s Neki-Aux ;
%<fut%>:%>s%>kej # ;
%<pres%>%<dur%>:to%>k # ;
%<impf%>%<dur%>:to%>ya # ;
%<impf%>%<dur%>:to%>ya%>j # ;
%<fut%>%<dur%>:to%>s # ;
%<fut%>%<dur%>:to%>s%>kej # ;
SufijoAuxiliar ;


LEXICON TAM-Clase2-a
%<pres%>:a # ;
%<pres%>:a%>j # ; ! TODO: flag diacritics for plural subj
%<pret%>:0 # ;
%<pret%>:%>kej # ;
%<impf%>:a%>ya # ;
%<impf%>:a%>ya%>j # ;
%<fut%>:a%>s # ;
a%>s Neki-Aux ;
%<fut%>:a%>s%>kej # ;
%<pres%>%<dur%>:to%>k # ;
%<impf%>%<dur%>:to%>ya # ;
%<impf%>%<dur%>:to%>ya%>j # ;
%<fut%>%<dur%>:to%>s # ;
%<fut%>%<dur%>:to%>s%>kej # ;
SufijoAuxiliar ;

LEXICON TAM-Clase2-i
%<pres%>:i # ;
%<pres%>:i%>j # ; ! TODO: flag diacritics for plural subj
%<pret%>:0 # ;
%<pret%>:%>kej # ;
%<impf%>:i%>ya # ;
%<impf%>:i%>ya%>j # ;
%<fut%>:i%>s # ;
i%>s Neki-Aux ;
%<fut%>:i%>s%>kej # ;
%<pres%>%<dur%>:%>to%>k # ;
%<impf%>%<dur%>:%>to%>ya # ;
%<impf%>%<dur%>:%>to%>ya%>j # ;
%<fut%>%<dur%>:%>to%>s # ;
%<fut%>%<dur%>:%>to%>s%>kej # ;
SufijoAuxiliar ;


LEXICON TAM-Clase3
%<pres%>:a # ;
%<pres%>:a%>j # ; ! TODO: flag diacritics for plural subj
%<pret%>:0 # ;
%<pret%>:j%>kej # ;
%<impf%>:a%>ya # ;
%<impf%>:a%>ya%>j # ;
%<fut%>:%>s # ;
%>s Neki-Aux ;
%<fut%>:%>s%>kej # ;
%<pres%>%<dur%>:j%>to%>k # ;
%<fut%>:j%>to%>kej # ;
%<impf%>%<dur%>:j%>to%>ya # ;
%<impf%>%<dur%>:j%>to%>ya%>j # ;
%<fut%>%<dur%>:j%>to%>s # ;
%<fut%>%<dur%>:j%>to%>s%>kej # ;
SufijoAuxiliar ;


LEXICON TAM-Clase4
%<pres%>:0 # ;
%<pres%>:a%>j # ;
%<pret%>: # ;
%<pret%>:j%>kej # ;
%<impf%>:a%>ya # ;
%<impf%>:a%>ya%>j # ;
%<fut%>:%>s # ;
%>s Neki-Aux ;
%<fut%>:%>s%>kej # ;
%<pres%>%<dur%>:j%>to%>k # ;
%<fut%>:j%>to%>kej # ;
%<impf%>%<dur%>:j%>to%>ya # ;
%<impf%>%<dur%>:j%>to%>ya%>j # ;
%<fut%>%<dur%>:j%>to%>s # ;
%<fut%>%<dur%>:j%>to%>s%>kej # ;
SufijoAuxiliar ;

LEXICON SufijoAuxiliar
Tinemi-Aux ;
Tijtia-Aux ;
Tiuits-Aux ;
Purposive ;

LEXICON Tinemi-Aux
%<aux%>%<pres%>:%>ti%>nemi # ;
%<aux%>%<impf%>:%>ti%>nemi%>ya # ;
%<aux%>%<impf%>:%>ti%>nemi%>ya%>j # ;
%<aux%>%<fut%>:%>ti%>nemi%>s # ;
%<aux%>%<fut%>:%>ti%>nemi%>s%>kej # ;
%<aux%>%<pres%>%<dur%>:%>ti%>nen%>to%>k # ;
%<aux%>%<impf%>%<dur%>:%>ti%>nen%>to%>ya # ;
%<aux%>%<impf%>%<dur%>:%>ti%>nen%>to%>ya%>j # ;
%<aux%>%<fut%>%<dur%>:%>ti%>nen%>to%>s # ;
%<aux%>%<fut%>%<dur%>:%>ti%>nen%>to%>s%>kej # ;

LEXICON Tijtia-Aux
%<aux%>%<pres%>:%>ti%>jtia # ;
%<aux%>%<impf%>:%>ti%>jtia%>ya # ;
%<aux%>%<impf%>:%>ti%>jtia%>ya%>j # ;
%<aux%>%<fut%>:%>ti%>jti%>s # ;
%<aux%>%<fut%>:%>ti%>jti%>s%>kej # ;
%<aux%>%<pres%>%<dur%>:%>ti%>jti%>to%>k # ;
%<aux%>%<impf%>%<dur%>:%>ti%>jti%>to%>ya # ;
%<aux%>%<impf%>%<dur%>:%>ti%>jti%>to%>ya%>j # ;
%<aux%>%<fut%>%<dur%>:%>ti%>jti%>to%>s # ;
%<aux%>%<fut%>%<dur%>:%>ti%>jti%>to%>s%>kej # ;

LEXICON Tiuits-Aux
%<aux%>%<pres%>:%>ti%>uits # ;
! TODO: figure out the rest of these (TAM)

LEXICON Neki-Aux
%<aux%>:neki # ;
%<aux%>:neki%>j # ;


LEXICON IV-Raiz

nejnemi%<iv%>:nejne%{M%}   TAM-Clase2-i ;
kochi%<iv%>:koch           TAM-Clase2-i ;
kalaki%<iv%>:kalak         TAM-Clase2-i ;
uetsi%<iv%>:uets           TAM-Clase2-i ;
miki%<iv%>:mik             TAM-Clase2-i ;
patlani%<iv%>:patlan       TAM-Clase2-i ;
nemi%<iv%>:ne%{m%}         TAM-Clase2-i ;

kisa%<iv%>:kis		   TAM-Clase2-a ;

palti%<iv%>:palti	   TAM-Clase1 ; ! "mojarse"

kualtia%<iv%>:kualti	   TAM-Clase3 ; ! "servir/estar bueno/no pudrido"
tlajtoa%<iv%>:tlajto	   TAM-Clase3 ; 


LEXICON TV-Raiz

kua%<tv%>:kua		   TAM-Clase4 ; ! "consumir/comer"
paleuia%<tv%>:paleui	   TAM-Clase3 ; ! "ayudar"

chiua%<tv%>:chiu           TAM-Clase2-a ; ! "hacer"
kaua%<tv%>:kau             TAM-Clase2-a ; ! "dejar"
koua%<tv%>:kou             TAM-Clase2-a ; ! "comprar"
chipaua%<tv%>:chipau	   TAM-Clase2-a ; ! "limpiar"
xipeua%<tv%>:xipeu	   TAM-Clase2-a ; ! "pelar"
paka%<tv%>:pak		   TAM-Clase2-a ; ! "lavar"

kaki%<tv%>:kak             TAM-Clase2-i ; ! "escuchar"
mati%<tv%>:mat             TAM-Clase2-i ; ! "saber"
teki%<tv%>:tek             TAM-Clase2-i ; ! "cortar"
tejteki%<tv%>:tejtek       TAM-Clase2-i ; ! "cortar varias veces"

namaka%<tv%>:namaka	   TAM-Clase1 ; ! "vender s.t."
tlasojtla%<tv%>:tlasojtla  TAM-Clase1 ; ! "amar"

LEXICON DV-Raiz
maka%<tv2%>:maka       	   TAM-Clase1 ; ! "dar"
iluia%<tv2%>:ilui	   TAM-Clase3 ; ! "decir"

