!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!



Multichar_Symbols
@D.Subj.3Pl@
@D.Obj.NotPl1@
@P.Subj.3Pl@
@R.SubjPl.1@
@R.Num.Unambig@
@P.Num.Unambig@
@D.Opt@
@R.Subj.3Pl@
@R.Opt@
@P.IsPast.1@
@D.IsPast@
@P.SubjPl.1@
@D.SubjPl@
@R.IsPast.1@
@P.Obj.NotPl1@
@P.Opt.1@

%<n%>                                    ! Sustantivo
%<v%>                                    ! Verbo
%<adj%>                                  ! Adjetivo
%<det%>                                  ! Determinante
%<num%>                                  ! Número
%<prn%>                                  ! Pronombre
%<adv%>                                  ! Adverbio
%<np%>                                   ! Nombre propio
%<cnjsub%>                               ! Coordinación subordinada

%<pr%>                                   ! Preposición

%<tv%>                                   ! Transitivo
%<tv2%>                                  ! Bitransitivo
%<iv%>                                   ! Intransitivo

%<pl%>                                   ! Plural
%<sg%>                                   ! Singular

%<abs%>                                  ! Absolutivo

%<loc%>                                  ! Locativo

%<top%>                                  ! Topónimo

%<pro%>                                  ! Pronombre proclítico
%<pers%>                                 ! Personal, forma larga

%<dim%>                                  ! Diminutivo

%<hon%>                                  ! Honorifico

%<barb%>                                 ! Barbarismo (español)


%<pres%>                                 ! tiempo presente
%<pret%>                                 ! pretérito
%<perf%>                                 ! perfectivo
%<impf%>                                 ! imperfectivo
%<fut%>                                  ! futuro
%<plu%>                                  ! pluscuamperfecto
%<opt%>                                  ! optativo
%<pst%>                                  ! pasado
%<dur%>                                  ! aspecto durativo

%<p1%>
%<p2%>
%<p3%>

%<px1sg%>                                ! Posesivo (1ra singular)
%<px2sg%>
%<px3sg%>
%<px1pl%>
%<px2pl%>
%<px3pl%>
%<px_nn%>				                 ! "te-"

%<s_sg1%>                                ! Marcador de sujeto (1ra singular)
%<s_sg2%>                                ! Marcador de sujeto (2da singular)
%<s_sg3%>
%<s_pl1%>
%<s_pl2%>
%<s_pl3%>

%<o_sg1%>                               ! Marcador de objeto directo (1ra singular)
%<o_sg2%>                               ! Marcador de objeto directo (2da singular)
%<o_sg3%>
%<o_pl1%>
%<o_pl2%>
%<o_pl3%>

%<i_sg1%>                               ! Marcador de objeto indirecto (1ra singular)
%<i_sg2%>                               ! Marcador de objeto indirecto (2da singular)
%<i_sg3%>
%<i_pl1%>
%<i_pl2%>
%<i_pl3%>

%<o_aa3%>
%<o_nn3%>

%<ven%>                           ! direccional hacia el locutor
%<and%>				              ! direccional no hacia el locutor

%<afirm%>
%<ant%>
%<appl%>
%<aux%>
%<cfact%>
%<cni%>
%<cm%>
%<cnjcoo%>
%<f%>
%<guio%>
%<ij%>
%<lpar%>
%<lquest%>
%<lquot%>
%<m%>
%<sen%>
%<yek%>
%<neg%>
%<o_sg3_om%>
%<part%>
%<refl%>
%<rel%>
%<rpar%>
%<rquot%>
%<bien%>
%<juntos%>
%<completamente%>
%<prx%>  ! Proximal ʼthisʼ
%<med%>  ! Medial "that"
%<dst%>  ! Distal "that (over there)"

%<sent%>                                 ! Final de frase

%{o%}                                    ! o 
%{i%}                                    ! i / 0
%{e%}                                    ! e / 0
%{h%}                                    ! h / u
%{u%}                                    ! u / h
%{H%}
%{n%}                                    ! m / 0
%{M%}                                    ! m / n
%{q%}
%{c%}
%{s%}                                    ! para plural español
%{z%}                                    ! z/c
%{e%}
%{%@%}                                   ! para terminaciones -o y -a en sustantivos y/o adjetivos
%{y%}					 ! y/x (ej pretérito de piya)
%{u%}
%>                                       ! Frontera de morfema

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON Root

Nominal ;
Aumento ;
Determinantes ;
Conj ; 

Numerales ; 
!Propios ;
Adverbios ;
!Preposiciones ;
!Puntuación ;
Interjecciones ; 
!Verb ;
! Digits ;


LEXICON Numerales
ce:ce         Numerales-Cont ;
ome:ome       Numerales-Cont ;
yeyi:yeyi     Numerales-Cont ;
naui:naui     Numerales-Cont ;
macuil:macuil Numerales-Cont ;


LEXICON Numerales-Cont
%<num%>:0 # ;


LEXICON Determinantes
in%<det%>:in # ;

LEXICON Adverbios
ijkuak:ijkuak                Adverbios-Cont ; ! "cuando"
kenman:kenman                Adverbios-Cont ;
mostla:mostla                Adverbios-Cont ;
kualkan:kualkan              Adverbios-Cont ;
yeuiptla:yeuiptla            Adverbios-Cont ;
axan:axan                    Adverbios-Cont ; 

kanin%<itg%>:kanin           Adverbios-Cont ; ! "dónde"
kanic%<itg%>:kanic           Adverbios-Cont ; ! "por dónde"
kan:kan                      Adverbios-Cont ; ! "donde"

san:san                      Adverbios-Cont ; ! "sólo, nada mas"
entontonces:tons             Adverbios-Cont ; ! "entonces"
entonces:tos                 Adverbios-Cont ; ! "entonces"
entonces:entonces            Adverbios-Cont ; ! "entonces"
amo:amo                      Adverbios-Cont ; ! "no"
ompa:ompa                    Adverbios-Cont ; ! "there"
nican:nican                  Adverbios-Cont ; ! "here"
niman:niman                  Adverbios-Cont ; ! "pronto"


LEXICON Adverbios-Cont
%<adv%>:0 # ;

LEXICON Interjecciones
kemaj:kemah Interjecciones-Cont ; ! "yes"
amo:amo     Interjecciones-Cont ; ! "no"

LEXICON Interjecciones-Cont
%<intj%>:0 # ;

LEXICON Conj
ihcuac:ihcuac       SubConj-Cont ;
tla:tla             SubConj-Cont ;
tlamo:tlamo         SubConj-Cont ; ! "si no"
hasta%<barb%>:hasta SubConj-Cont ;
nic:nic             SubConj-Cont ;

uan:uan             CooConj-Cont ; 

LEXICON SubConj-Cont
%<cnjsub%>:0 # ;

LEXICON CooConj-Cont
%<cnjcoo%>:0 # ;


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!
! Nominales
!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON Nominal

Pronombre ;
SujetoNominal ;

LEXICON Pronombre

PronombrePersonal ;
PronombreDemonstrativo ;
OtrosPronombres ; 


LEXICON PronombrePersonal

! Singular
nejuatl%<prn%>%<pro%>%<p1%>%<sg%>:ne       # ;
nejuatl%<prn%>%<pro%>%<p1%>%<sg%>:nej       # ;
nejuatl%<prn%>%<pers%>%<p1%>%<sg%>:nejuatl # ;
tejuatl%<prn%>%<pro%>%<p2%>%<sg%>:te       # ;
tejuatl%<prn%>%<pro%>%<p2%>%<sg%>:tej       # ;
tejuatl%<prn%>%<pers%>%<p2%>%<sg%>:tejuatl # ;
yejuatl%<prn%>%<pro%>%<p3%>%<sg%>:ye       # ;
yejuatl%<prn%>%<pro%>%<p3%>%<sg%>:yej       # ;
yejuatl%<prn%>%<pers%>%<p3%>%<sg%>:yejuatl # ;

! Plural
tejuan%<prn%>%<pers%>%<p1%>%<pl%>:tejuan     # ;
namejuan%<prn%>%<pers%>%<p2%>%<pl%>:namejuan # ;
yejuan%<prn%>%<pers%>%<p3%>%<pl%>:yejuan     # ;

LEXICON PronombreDemonstrativo
nin%<prn%>%<prx%>:nin            # ; ! "esto"
nin%<prn%>%<prx%>%<pl%>:nin%>mej # ; ! "estos"

non%<prn%>%<med%>:non            # ; ! "eso"
non%<prn%>%<med%>%<pl%>:non%>mej # ; ! "esos"

neka%<prn%>%<dst%>:neka          # ; ! "aquello"
neka%<prn%>%<dst%>%<pl%>:nekatej # ; ! "aquellos"

LEXICON OtrosPronombres
akin%<prn%>:aquin # ; ! quién
tlen%<prn%>:tlen  # ; ! que


LEXICON SujetoNominal

%<s_sg1%>:ni%>      PrefijoPosesivo ;
%<s_sg2%>:ti%>      PrefijoPosesivo ;
%<s_sg3%>:0         PrefijoPosesivo ;
@P.SubjPl.1@%<s_pl1%>:@P.SubjPl.1@t%{i%}%>  PrefijoPosesivo ; ! 
@P.SubjPl.1@%<s_pl2%>:@P.SubjPl.1@nan%>     PrefijoPosesivo ; ! 
@P.SubjPl.1@%<s_pl3%>:@P.SubjPl.1@0         PrefijoPosesivo ; ! 

LEXICON PrefijoPosesivo
Sustantivo ;
%<px1sg%>:no%> Sustantivo ;
%<px2sg%>:mo%> Sustantivo ;
%<px3sg%>:i%> Sustantivo ;
%<px1pl%>:to%> Sustantivo ;
%<px2pl%>:namo%> Sustantivo ;
! %<px3pl%>:in%{m%}%{i%}%> Sustantivo ; 
%<px3pl%>:imin%> Sustantivo ;
%<px3pl%>:i%{n%}%> Sustantivo ;

%<px_nn%>:te Sustantivo ; ! Todo: figure out correct tag

LEXICON PluralNominal
%<abs%>%<pl%>:%>mej # ;
%<pl%>:%>uan        # ;

LEXICON TratoFamiliar
%<dim%>:%>tsin            # ;
%<dim%>:%>tsin%>tli       # ;
%<dim%>%<pl%>:%>tsi%>tsin # ;
%<dim%>%<pl%>:%>tsin%>uan # ; ! poseído


LEXICON TL
%<n%>%<abs%>%<sg%>:%>tl       # ;
%<n%>%<sg%>:0                 # ; ! poseído TODO: flag diacritics ftw
%<n%>:0                       TratoFamiliar ;
%<n%>:0                       PluralNominal ;

LEXICON TLI
%<n%>%<abs%>%<sg%>:%>tli      # ;
%<n%>%<sg%>:0                 # ;
%<n%>:0                       TratoFamiliar ;
%<n%>:0                       PluralNominal ;

LEXICON LI
%<n%>%<abs%>%<sg%>:%>i        # ;
%<n%>%<sg%>:0                 # ;
%<n%>:0                       TratoFamiliar ;
%<n%>:0                       PluralNominal ;

LEXICON NA
%<n%>%<sg%>:0                 # ;
%<n%>:0                       TratoFamiliar ;
%<n%>:0                       PluralNominal ;

LEXICON Adjetivo
kualli:kualli    Adjetivo-Cont ;  ! "good"

LEXICON Adjetivo-Cont
%<adj%>:0 # ; 


LEXICON SusRel
ca:ca          SusRel-Cont ;
uan:uan        SusRel-Cont ;
nauac:nauac    SusRel-Cont ;
pan:pan        SusRel-Cont ;
tenco:tenco    SusRel-Cont ;


LEXICON SusRel-Cont
%<n%>%<rel%>:0 TratoFamiliar ;
%<n%>%<rel%>:0 #;

LEXICON Sustantivo
Adjetivo ;
SusRel ; ! Relational Nouns

axka:axka                       NA; ! "partícula posesiva"
tlakual:tlakual                 NA; ! "comida"
miston:miston                   NA; ! "gato"
tlaol:tlaol                     NA; ! "maíz"
yoal:yoal                       NA; ! "noche"
tlamachtiloyan:tlamachtiloyan   NA; ! "maíz"
tonal:tonal                     NA; ! "día"

tlaxkali:tlaxkal    LI ; ! "tortilla"
pilli:pil           LI ; ! "child"
kalli:kal           LI ; ! "house"

atl:a                       TL ; ! "hormiga"
askatl:aska                 TL ; ! "hormiga"
altepetl:altepe             TL ; ! "pueblo"
koyotl:koyo                 TL ; ! "coyote"
koatl:koa                   TL ; ! "víbora"
tlakatl:tlaka               TL ; ! "hombre"
soatl:soa                   TL ; ! "mujer"
pitsotl:pitso               TL ; ! "puerco"
kuauitl:kuaui               TL ; ! "árbol, madera"
konetl:kone                 TL ; ! "baby/child"
kueitl:kuei                 TL ; ! "skirt"
kuikatl:kuika               TL ; ! "song"
uejxolotl:uejxolo           TL ; ! "turkey"
ichkatl:ichka               TL ; ! "sheep"
iluitl:ilui                 TL ; ! "party"
masatl:masa                 TL ; ! "venado"
mometl:mome                 TL ; ! ""
tlatsakuilotl:tlatsakuilo   TL ; ! ""
xaxokotl:xaxoko             TL ; ! "guayaba"
xonakatl:xonakatl           TL ; ! "onion"
yetl:ye                     TL ; ! ""
yolkatl:yolka               TL ; ! "horse/animal" TODO check in def in this variant
chikiuitl:chikiui           TL ; ! "basket"
komitl:komi                 TL ; ! "olla"
metlatl:metla               TL ; ! "metate"
petlatl:petla               TL ; ! "petate"
tetl:te                     TL ; ! "rock"
xochitl:xochi               TL ; ! "floer"
tepetl:tepe                 TL ; ! "hill/mountain"
piotl:pio                   TL ; ! "chicken"

textli:tex          TLI ; ! "masa"
tochtli:toch        TLI ; ! "conejo"
chantli:chan        TLI ; ! "puerco"
tepostli:tepos      TLI ; ! "metal, car, machine"
pajtli:paj          TLI ; ! "medicine"
nektli:nek          TLI ; ! "pulque?"
apastli:apas        TLI ; ! "hunger?"


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!
! Los Verbos
!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON Aumento

@P.IsPast.1@0:@P.IsPast.1@o%>           SujetoVerbal ; ! 
@P.IsPast.1@ya%<adv%>+:@P.IsPast.1@yo%> SujetoVerbal ; ! 
                SujetoVerbal ;


LEXICON SujetoVerbal

%<s_sg1%>:n%{i%}%>   	                         SujetoCont ;
@P.Opt.1@%<s_sg1%>:@P.Opt.1@n%{i%}%>                               SujetoCont ; ! 

%<s_sg2%>:t%{i%}%>  	                         SujetoCont ;
%<s_sg3%>:0%>     	                             SujetoCont ;
@P.Opt.1@%<s_sg3%>:@P.Opt.1@0%>                                    SujetoCont ; ! 

@P.SubjPl.1@%<s_pl1%>:@P.SubjPl.1@t%{i%}%>                               SujetoCont ; ! 
@P.Opt.1@@P.SubjPl.1@%<s_pl1%>:@P.Opt.1@@P.SubjPl.1@t%{i%}%>                               SujetoCont ; !  

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Cuando el sujeto es 2 plural (usteded), y el objeto es 1 plural (a nosotros), hay un tipo de harmonía entre 
! los consonantes, y la n del prefijo de sujeto se convierte en t.
!
@P.Num.Unambig@@P.Obj.NotPl1@@P.SubjPl.1@%<s_pl2%>:@P.Num.Unambig@@P.Obj.NotPl1@@P.SubjPl.1@nan%>                                  SujetoCont ; !   
@P.Num.Unambig@@P.SubjPl.1@%<s_pl2%>:@P.Num.Unambig@@P.SubjPl.1@tan%>                                  SujetoCont ; !  
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!


@P.Subj.3Pl@@P.SubjPl.1@%<s_pl3%>:@P.Subj.3Pl@@P.SubjPl.1@0%>                                    SujetoCont ; !  
@P.Subj.3Pl@@P.Opt.1@@P.SubjPl.1@%<s_pl3%>:@P.Subj.3Pl@@P.Opt.1@@P.SubjPl.1@0%>                                    SujetoCont ; !   

@P.Opt.1@%<s_sg2%>:@P.Opt.1@x%{i%}%>                               SujetoCont ; ! 
@P.Opt.1@@P.SubjPl.1@%<s_pl2%>:@P.Opt.1@@P.SubjPl.1@x%{i%}%>                               SujetoCont ; !  


LEXICON SujetoCont
IV-Dir ;
Objeto ;


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!
! Intransitive morphemes (lexicon at the end)
!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
LEXICON IV-Dir
%<and%>:on IV-Adv ;
%<ven%>:ual IV-Adv ;
IV-Adv ;

LEXICON IV-Adv
%<yek%>:yek IV-Raiz ;
%<sen%>:sen IV-Raiz ;
%<pan%>:pan IV-Raiz ;
! ...etc.
IV-Raiz ;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Objects for transitive and titransitive verbs
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
LEXICON Objeto
%<o_sg1%>:nech%>    TV-Dir ;
%<o_sg2%>:mits%>    TV-Dir ;
%<o_sg3%>:k%{i%}%>  TV-Dir ;
@D.Obj.NotPl1@%<o_pl1%>:@D.Obj.NotPl1@tech%>    TV-Dir ; ! 
@D.Subj.3Pl@%<o_pl2%>:@D.Subj.3Pl@amech%>   TV-Dir ; ! según flores-nájera 
@R.Subj.3Pl@%<o_pl2%>:@R.Subj.3Pl@namech%>  TV-Dir ; ! con sujeto de tercera persona.  
%<o_pl3%>:ki%{m%}%> TV-Dir ;

TV-Dir ;

DV-IO ;
DV-Dir ;

LEXICON DV-IO
%<i_sg1%>:nech%>    DV-O ;
%<i_sg2%>:mits%>    DV-O ;
%<i_sg3%>:k%{i%}%>  DV-O ;
%<i_pl1%>:tech%>    DV-O ;
%<i_pl2%>:mech%>    DV-O ;
%<i_pl2%>:namech%>  DV-O ; ! con sujeto de tercera persona.
%<i_pl3%>:ki%{m%}%> DV-O ;


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Transitive (1-object complement) morphemes
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
LEXICON TV-Dir
%<and%>:on TV-Refl ;
%<ven%>:ual TV-Refl ;
TV-Refl ;

LEXICON TV-Refl
%<refl%>:mo%>  TV-Adv ;
TV-Adv ;

!TODO: resolver dudas sobre la posicón y función de te- y tla- en ncx
LEXICON TV-Te
%<o_aa3%>:te%> TV-Raiz ;
TV-Raiz ;

LEXICON TV-Adv
%<yek%>:yek TV-Adv-Cont ;
%<sen%>:sen TV-Adv-Cont ;
%<pan%>:sen TV-Adv-Cont ;
! ...etc.
TV-Adv-Cont ;

LEXICON TV-Adv-Cont
TV-Tla ;
TV-Te ;
TV-Raiz ;

LEXICON TV-Tla
%<o_nn3%>:tla%> TV-Raiz ;


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Ditransitive (2 objects) morphemes.
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON DV-O
! for direct obj "im-" ... is this a thing in ncx?
DV-Dir ;

LEXICON DV-Dir
%<and%>:on DV-Refl ;
%<ven%>:ual DV-Refl ;
DV-Refl ;

LEXICON DV-Refl
%<refl%>:mo%>  DV-Adv ;
DV-Adv ;

LEXICON DV-Te
%<o_aa3%>:te%> DV-Raiz ;

LEXICON DV-Tla
%<o_nn3%>:tla%> DV-Raiz ;

LEXICON Te-Tla
%<i_aa3%>%<o_nn3%>:te%>tla%> DV-Raiz ;

LEXICON DV-Adv
%<yek%>:yek DV-Adv-Cont ;
%<sen%>:sen DV-Adv-Cont ;
%<pan%>:sen DV-Adv-Cont ;
! ...etc.
DV-Adv-Cont ;

LEXICON DV-Adv-Cont
Te-Tla  ;
DV-Te   ;
DV-Tla  ;
DV-Raiz ;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! TAM stuff for Verbs
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON Agr-Pl-Past
@D.Opt@@R.SubjPl.1@@R.IsPast.1@:@D.Opt@@R.SubjPl.1@@R.IsPast.1@ # ; !   

LEXICON Agr-Sg-Past
@R.IsPast.1@@D.Opt@@D.SubjPl@:@R.IsPast.1@@D.Opt@@D.SubjPl@ # ; !   

LEXICON Agr-Pl-Nonpast
@D.IsPast@@D.Opt@@R.SubjPl.1@:@D.IsPast@@D.Opt@@R.SubjPl.1@ # ; !   

LEXICON Agr-Sg-Nonpast
@D.IsPast@@D.Opt@@D.SubjPl@:@D.IsPast@@D.Opt@@D.SubjPl@ # ; !   

LEXICON Agr-Sg-Opt
@R.Opt@@D.SubjPl@:@R.Opt@@D.SubjPl@ # ; !  

LEXICON Agr-Pl-Opt
@R.Opt@@R.SubjPl.1@:@R.Opt@@R.SubjPl.1@ # ; !  

LEXICON PresentTense
%<pres%>:0     Agr-Sg-Nonpast ;
%<pres%>:%>j   Agr-Pl-Nonpast ;
@R.Num.Unambig@%<pres%>:@R.Num.Unambig@0     Agr-Pl-Nonpast ; ! 

LEXICON PreteriteK
%<pret%>:%>k   Agr-Sg-Past ;
%<pret%>:%>kej Agr-Pl-Past ;

LEXICON PreteriteJ
%<pret%>:      Agr-Sg-Past ; ! Dir/LR
%<pret%>:%>j   Agr-Sg-Past ;
%<pret%>:%>kej Agr-Pl-Past ;

LEXICON PreteriteNull
%<pret%>:      Agr-Sg-Past ;
%<pret%>:%>kej Agr-Pl-Past ;

LEXICON Imperfect
%<impf%>:%>ya Agr-Sg-Past ;
%<impf%>:%>ya%>j Agr-Pl-Past ;

LEXICON Future
%<fut%>:%>s Agr-Sg-Nonpast ;
%<fut%>:%>s%>kej Agr-Pl-Nonpast ;
%<fut%>:%>s Neki-Aux ;

LEXICON Durative
%<dur%>%<pres%>:%>to%>k Agr-Sg-Nonpast ;
%<dur%>%<pres%>:%>to%>k%>ej Agr-Pl-Nonpast ;
%<dur%>:%>to Imperfect ;
%<dur%>:%>to Future ;

LEXICON Optative
%<opt%>:0 Agr-Sg-Opt ;
%<opt%>:%>kan Agr-Pl-Opt ;

LEXICON Conditional
%<cond%>:%>skia    Agr-Sg-Past ;
%<cond%>:%>skia%>j Agr-Pl-Past ;

LEXICON TAM-Clase1
        PresentTense ;
        PreteriteK ;
        Imperfect ;
        Future ;
        Conditional ;
        Durative ;
        Optative ;
        SufijoAuxiliar ;

LEXICON TAM-Panoa
0:a       PresentTense ;
          PreteriteK ;
0:a       Imperfect ;
          Future ;
          Conditional ;
          Durative ;
          Optative ;
          SufijoAuxiliar ;

LEXICON TAM-Ita
0:ta      PresentTense ;
0:ta      PreteriteK ;
0:ta      Imperfect ;
0:ta      Future ;
          Conditional ;
0:s       Durative ;
0:a       Optative ;
0:a       SufijoAuxiliar ;

LEXICON TAM-Clase2-a
0:a       PresentTense ;
          PreteriteNull ;
0:a       Imperfect ;
0:a       Future ;
0:a       Conditional ;
          Durative ;
0:a       Optative ;
          SufijoAuxiliar ;

LEXICON TAM-Clase2-i
0:i       PresentTense ;
          PreteriteNull ;
0:i       Imperfect ;
0:i       Future ;
0:i       Conditional ;
          Durative ;
0:i       Optative ;
          SufijoAuxiliar ;

LEXICON TAM-Clase3
0:a       PresentTense ;
          PreteriteJ ;
0:a       Imperfect ;
          Future ;
          Conditional ;
0:j       Durative ;
          Optative ;
          SufijoAuxiliar ;

LEXICON TAM-Clase4
          PresentTense ;
          PreteriteJ ;
          Imperfect ;
          Future ;
          Conditional ;
0:j       Durative ;
          Optative ;
          SufijoAuxiliar ;

LEXICON SufijoAuxiliar
Tinemi-Aux ;
Tijtia-Aux ;
Tiuits-Aux ;
Purposive ;

LEXICON Tinemi-Aux
%+%<aux%>nemi:%>ti%>nemi        PresentTense ;
%+%<aux%>nemi:%>ti%>nemi        Imperfect ;
%+%<aux%>nemi:%>ti%>nemi        Future ;
%+%<aux%>nemi:%>ti%>nen         Durative ;

LEXICON Tijtia-Aux ! "hacer algo poco a poquito"
%+%<aux%>ijtia:%>ti%>jtia        PresentTense ;
%+%<aux%>ijtia:%>ti%>jtia        Imperfect ;
%+%<aux%>ijtia:%>ti%>jti         Future ;
%+%<aux%>ijtia:%>ti%>jti         Durative ;

LEXICON Tiuits-Aux
%+uits%<aux%>:%>ti%>uits      Agr-Sg-Nonpast ;
%+uits%<aux%>:%>ti%>uits%>ej  Agr-Pl-Nonpast ;
%+uits%<aux%>:%>ti%>uits%>e   Agr-Pl-Nonpast ; ! Dir/LR

LEXICON Neki-Aux
%+neki%<aux%>:%>neki  PresentTense ;

LEXICON Purposive
%<pres%>%<and%>:%>ti Agr-Sg-Nonpast ;
%<pres%>%<and%>:%>ti%>j Agr-Pl-Nonpast ; ! Dir/LR
%<pres%>%<and%>:%>ti%>ue Agr-Pl-Nonpast ;
%<pret%>%<and%>:%>to Agr-Sg-Past ;
%<pret%>%<and%>:%>to%>j Agr-Pl-Past ;

%<pres%>%<ven%>:%>ki     Agr-Sg-Nonpast ;
%<pres%>%<ven%>:%>ki%>j  Agr-Sg-Nonpast ; ! Dir/LR
%<pres%>%<ven%>:%>ki%>ue Agr-Sg-Nonpast ;
%<pret%>%<ven%>:%>ko     Agr-Sg-Past ;
%<pret%>%<ven%>:%>ko%>j  Agr-Sg-Past ;


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Verb lexica
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON IV-Raiz

ajsi%<iv%>:ajsi                         TAM-Clase1 ; ! "llegar thitherly"
palti%<iv%>:palti                       TAM-Clase1 ; ! "mojarse"
tlejko%<iv%>:tlehko                     TAM-Clase1 ; !" climb, ascend"
ejko%<iv%>:ejko                         TAM-Clase1 ; !" arrive hitherly"
tsajtsi%<iv%>:tsajtsi                   TAM-Clase1 ; ! "yell"
choka%<iv%>:choka                       TAM-Clase1 ; ! "cry"
panoa%<iv%>:pano                        TAM-Panoa  ; ! "pasar/suceder"


nejnemi%<iv%>:nejne%{M%}                TAM-Clase2-i ; ! "walk"
kualani%<iv%>:kualan                    TAM-Clase2-i ; ! "get angry"
kalaki%<iv%>:kalak                      TAM-Clase2-i ; ! "enter"
kochi%<iv%>:koch                        TAM-Clase2-i ; ! "sleep"
uetsi%<iv%>:uets                        TAM-Clase2-i ; ! "fall"
miki%<iv%>:mik                          TAM-Clase2-i ; ! "die"
paqui%<iv%>:pak                         TAM-Clase2-i ; ! "be happy"
patlani%<iv%>:patlan                    TAM-Clase2-i ; ! "fly"
nemi%<iv%>:ne%{m%}                      TAM-Clase2-i ; ! "go around (andar)"
tsekuini%<iv%>:tsekuin                  TAM-Clase2-i ; ! "jump"
poliui%<iv%>:poliu                      TAM-Clase2-i;  ! "get lost"

pinaua%<iv%>:pina%{u%}                  TAM-Clase2-a ; ! "be ashamed"
peua%<iv%>:pe%{u%}                      TAM-Clase2-a ; ! "start
kisa%<iv%>:kis                          TAM-Clase2-a ; ! "emerge, go out"

kualtia%<iv%>:kualti	                TAM-Clase3 ; ! "servir/estar bueno/no pudrido"
tlajtoa%<iv%>:tlajto	                TAM-Clase3 ; ! "hablar"
choloa%<iv%>:cholo                      TAM-Clase3 ; ! "flee"
chantia%<iv%>:chanti	                TAM-Clase3 ; ! "vivir"


ueli%<iv%>:ueli		       TAM-Clase4 ; ! TODO is this even correct


LEXICON TV-Raiz

namaka%<tv%>:namaka	       TAM-Clase1 ; ! "vender s.t."
tlaksa%<tv%>:tlaksa        TAM-Clase1 ; ! "step on (pisar)"
tlasojtla%<tv%>:tlasojtla  TAM-Clase1 ; ! "amar"
ajua%<tv%>:ajua            TAM-Clase1 ; ! "scold"
uika%<tv%>:uika            TAM-Clase1 ; ! "llevar"
ualika%<tv%>:ualika        TAM-Clase1 ; ! "traer"

panoa%<tv%>:pano           TAM-Panoa ; ! "pasar (a alguien/algo)"
ita%<tv%>:it		       TAM-Ita ; !"ver"

chiua%<tv%>:chi%{u%}       TAM-Clase2-a ; ! "hacer"
ilkaua%<tv%>:ilkau         TAM-Clase2-a ; ! "forget"
chiya%<tv%>:chi%{y%}       TAM-Clase2-a ; ! "wait for"
eua%<tv%>:eu               TAM-Clase2-a ; ! "raise, lift, get up (e.g. from bed)"
kaua%<tv%>:ka%{u%}         TAM-Clase2-a ; ! "dejar"
koua%<tv%>:kou             TAM-Clase2-a ; ! "comprar"
kuepa%<tv%>:kuep           TAM-Clase2-a ; ! "voltear"
chipaua%<tv%>:chipau	   TAM-Clase2-a ; ! "limpiar"
xipeua%<tv%>:xipeu	       TAM-Clase2-a ; ! "pelar"
paka%<tv%>:pak		       TAM-Clase2-a ; ! "lavar"
piya%<tv%>:pi%{y%}	       TAM-Clase2-a ; ! "tener"
notsa%<tv%>:nots           TAM-Clase2-a ; ! "call"

kaki%<tv%>:kak             TAM-Clase2-i ; ! "escuchar"
nauateki%<tv%>:nauatek     TAM-Clase2-i ; ! "embrace"
ilnamiki%<tv%>:ilnamik     TAM-Clase2-i ; ! "remember"
mati%<tv%>:mat             TAM-Clase2-i ; ! "saber"
ixmati%<tv%>:ixmat         TAM-Clase2-i ; ! "conocer"
teki%<tv%>:tek             TAM-Clase2-i ; ! "cortar"
tejteki%<tv%>:tejtek       TAM-Clase2-i ; ! "cortar varias veces"
neki%<tv%>:nek             TAM-Clase2-i ; ! "querer"

paleuia%<tv%>:paleui	   TAM-Clase3 ; ! "ayudar"
kualania%<tv%>:kualani     TAM-Clase3 ; ! "anger"
majtia%<tv%>:majti         TAM-Clase3 ; ! "frighten"
miktia%<tv%>:mikti         TAM-Clase3 ; ! "kill"
pakiltia%<tv%>:pakilti     TAM-Clase3 ; ! "make happy, cheer up"
kixtia%<tv%>:kixti         TAM-Clase3 ; ! "take out, remove"
paktia%<tv%>:pakti	       TAM-Clase3 ; ! "hacer feliz / gustar"
tlakualtia%<tv%>:tlakualti TAM-Clase3 ; ! "feed"
ijtoa%<tv%>:ijto           TAM-Clase3 ; ! decir"
tlalia%<tv%>:tlali	       TAM-Clase3 ; ! "poner 
machtia%<tv%>:machti	   TAM-Clase3 ; ! "enseñar/hacer saber"
ijtotia%<tv%>:ijtoti       TAM-Clase3 ; ! "dance"
nankilia%<n%>:nankili      TAM-Clase3 ; ! "contestar"

kua%<tv%>:kua		       TAM-Clase4 ; ! "consumir/comer"

LEXICON DV-Raiz
maka%<tv2%>:maka       	   TAM-Clase1 ; ! "dar"

ilia%<tv2%>:ili	           TAM-Clase3 ; ! "decir"

